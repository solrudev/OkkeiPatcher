plugins {
    id "com.android.application"
    id "kotlin-android"
    id "kotlin-kapt"
    id "org.jetbrains.kotlin.plugin.serialization" version "$kotlin_version"
    id "dagger.hilt.android.plugin"
}

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "solru.okkeipatcher"
        minSdkVersion 19
        targetSdkVersion 30
        versionCode 42
        versionName "2.0"
        archivesBaseName = "$applicationId"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            keyAlias keystoreProperties["keyAlias"]
            keyPassword keystoreProperties["keyPassword"]
            storeFile file(keystoreProperties["storeFile"])
            storePassword keystoreProperties["storePassword"]
            enableV3Signing = true
            enableV4Signing = true
        }
    }

    buildTypes {
        debug {
            multiDexEnabled true
        }
        release {
            minifyEnabled true
            shrinkResources true
            signingConfig signingConfigs.getByName("release")
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
        }
        releaseNoMinify {
            minifyEnabled false
            multiDexEnabled true
            signingConfig signingConfigs.getByName("release")
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
        }
        releaseMinifiedNoObfuscation {
            postprocessing {
                removeUnusedCode true
                removeUnusedResources true
                obfuscate false
                optimizeCode true
                proguardFiles = ["proguard-rules.pro"]
            }
            signingConfig signingConfigs.getByName("release")
        }
    }

    aaptOptions {
        noCompress "pk8"
        noCompress "past"
    }

    compileOptions {
        sourceCompatibility "1.8"
        targetCompatibility "1.8"
    }

    packagingOptions {
        exclude "DebugProbesKt.bin"
        exclude "androidsupportmultidexversion.txt"
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    buildFeatures {
        dataBinding true
        viewBinding true
    }
}

dependencies {
    def lifecycle_version = "2.3.1"
    def coroutines_version = "1.5.1"
    def workmanager_version = "2.6.0-beta02"

    kapt "androidx.databinding:databinding-compiler:7.0.0"
    kapt "com.google.dagger:hilt-compiler:$hilt_version"
    kapt "androidx.hilt:hilt-compiler:1.0.0"

    implementation "com.google.dagger:hilt-android:$hilt_version"
    implementation "androidx.core:core-ktx:1.6.0"
    implementation "androidx.activity:activity-ktx:1.3.0"
    implementation "androidx.appcompat:appcompat:1.3.1"
    implementation "com.google.android.material:material:1.4.0"
    implementation "androidx.constraintlayout:constraintlayout:2.1.0"
    implementation "androidx.preference:preference-ktx:1.1.1"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.fragment:fragment-ktx:1.3.6"
    implementation "androidx.documentfile:documentfile:1.0.1"
    implementation "androidx.hilt:hilt-work:1.0.0"
    implementation "androidx.work:work-runtime:$workmanager_version"
    implementation "androidx.work:work-runtime-ktx:$workmanager_version"
//    implementation "androidx.multidex:multidex:2.0.1"
    implementation 'org.jetbrains.kotlinx:kotlinx-serialization-json:1.2.2'
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"
    implementation project(path: ":ktor-client-okhttp312")
    implementation "io.ktor:ktor-client-core-jvm:1.6.2"
    implementation "com.squareup.okio:okio:$okio_version"
    implementation "com.github.aefyr:pseudoapksigner:1.6"
    implementation 'com.anggrayudi:storage:0.11.1'
    implementation "net.lingala.zip4j:zip4j:2.9.0"

    testImplementation "junit:junit:4.13.2"
    androidTestImplementation "androidx.test.ext:junit:1.1.3"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.4.0"
}